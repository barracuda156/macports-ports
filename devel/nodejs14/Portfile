# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               compiler_blacklist_versions 1.0
PortGroup               legacysupport 1.1
PortGroup               openssl 1.0
PortGroup               deprecated 1.0

# EOL 2023-04-30
# https://github.com/nodejs/Release
deprecated.eol_version  yes

# Peg back to openssl 1.1 release
openssl.branch          1.1
openssl.configure       pkgconfig build_flags

# on macOS nodejs only builds against libc++
# this force is OK as node does not link against any other c++ libs
if {${os.arch} ne "powerpc"} {
    depends_lib-append      port:libcxx
    configure.cxx_stdlib    libc++
    use_xcode               yes
}
compiler.cxx_standard   2014

name                    nodejs14
version                 14.21.3
revision                1

categories              devel net
license                 {MIT BSD}
maintainers             {ciserlohn @ci42} openmaintainer

description             Evented I/O for V8 JavaScript

long_description        Node's goal is to provide an easy way to build scalable network programs in JavaScript. \
                        Node is similar in design to and influenced by systems like Ruby's Event \
                        Machine or Python's Twisted. Node takes the event model a bit further-it \
                        presents the event loop as a language construct instead of as a library.

conflicts               nodejs8 nodejs10 nodejs12 nodejs13 nodejs15 nodejs16 nodejs17 nodejs18 nodejs19 nodejs20 nodejs21

homepage                https://nodejs.org/
master_sites            ${homepage}dist/v${version}
use_xz                  yes

checksums               rmd160  fb95405a2acaee5064a021e2dc8a6a6493964360 \
                        sha256  458ec092e60ad700ddcf079cb63d435c15da4c7bb3d3f99b9a8e58a99e54075e \
                        size    34508116

distname                node-v${version}

depends_build-append    port:pkgconfig

set py_ver              3.10
set py_ver_nodot        [string map {. {}} ${py_ver}]

depends_lib-append      path:lib/pkgconfig/icu-uc.pc:icu \
                        port:python${py_ver_nodot} \
                        port:zlib

proc rec_glob {basedir pattern} {
    set files [glob -directory $basedir -nocomplain -type f $pattern]
    foreach dir [glob -directory $basedir -nocomplain -type d *] {
        lappend files {*}[rec_glob $dir $pattern]
    }
    return $files
}

configure.python        ${prefix}/bin/python${py_ver}

if {${os.platform} eq "darwin" && (${configure.build_arch} in [list ppc ppc64])} {
    patchfiles-append   0001-Restore-ppc32-patches-from-upstream.patch \
                        0002-inspector-unbreak-build-with-gcc.patch \
                        0003-platform-posix.cc-fix-macOS.patch \
                        0004-libsampler-support-powerpc-darwin.patch \
                        0005-cpu.cc-support-powerpc-darwin.patch \
                        0006-semaphore-use-libdispatch-only-where-it-is-available.patch \
                        0007-register-ppc.h-amend-register-specification-for-Darw.patch \
                        0008-c-linkage.cc-amend-register-specification-for-Darwin.patch \
                        0009-fix-build-system.patch \
                        0010-cpu.cc-fix-for-8.4.patch \
                        0011-runtime-utils.h-fix-a-typo.patch \
                        0012-logging.h-v8-string-conversions.h-add-missing-includ.patch \
                        0013-instruction-selector-ppc.cc-add-omitted-fix.patch \
                        0014-code-generator-ppc.cc-some-ppc-assembler-fixes.patch \
                        0015-code-generator-ppc.cc-fallback-for-clrldi.patch \
                        0016-Experimental-fixes-for-ppc.patch \
                        0017-semaphores-temporary-hack-for-sem_timedwait.patch \
                        0018-Do-not-try-using-unsupported-simd.patch
    post-patch {
        reinplace "s|@DEPLOYMENT_TARGET@|${macosx_deployment_target}|g" ${worksrcpath}/common.gypi
        reinplace "s|@PREFIX@|${prefix}|g" ${worksrcpath}/common.gypi
        reinplace "s|@MACOS@|${os.major}|" ${worksrcpath}/deps/cares/config/darwin/ares_config.h
        if {${configure.build_arch} eq "ppc64"} {
            reinplace "s|@ARCH@|powerpc64|" ${worksrcpath}/deps/cares/config/darwin/ares_config.h
        } else {
            reinplace "s|@ARCH@|powerpc|" ${worksrcpath}/deps/cares/config/darwin/ares_config.h
        }
    }
} else {
    patchfiles-append   patch-common.gypi.diff
}

post-patch {
    foreach f [concat ${worksrcpath}/configure \
                   ${worksrcpath}/tools/gyp/gyp \
                   ${worksrcpath}/deps/v8/tools/objdump-v8 \
                   [rec_glob ${worksrcpath} *.py]] {
        reinplace -q "s|/usr/bin/env python3|${configure.python}|" ${f}
        reinplace -q "s|/usr/bin/env python|${configure.python}|" ${f}
    }
    foreach gypfile [rec_glob ${worksrcpath} *.gyp*] {
        reinplace -q "s|'python3'|'${configure.python}'|" ${gypfile}
        reinplace -q "s|'python'|'${configure.python}'|" ${gypfile}
    }
}

# use the system libuv instead of the bundled version, as it is fixed for older systems
if { ${os.platform} eq "darwin" && ${os.major} < 11 } {
    depends_lib-append    path:lib/libuv.dylib:libuv
    configure.args-append --shared-libuv
}

pre-configure {
     # Copy zlib headers here because we do not want to prepend the entire
     # ${prefix}/include to the include path (the build will then attempt to use
     # ICU 67 headers)
     file mkdir ${workpath}/zlib-inc
     file copy ${prefix}/include/zconf.h ${prefix}/include/zlib.h \
         ${workpath}/zlib-inc/
}

configure.args-append   --without-npm
configure.args-append   --with-intl=system-icu
configure.args-append   --shared-openssl
configure.args-append   --shared-openssl-includes=[openssl::include_dir]
configure.args-append   --shared-openssl-libpath=[openssl::lib_dir]
configure.args-append   --shared-zlib
configure.args-append   --shared-zlib-includes=${workpath}/zlib-inc
configure.args-append   --shared-zlib-libpath=${prefix}/lib

if {${configure.build_arch} eq "ppc"} {
    # The problem we face now is insufficient address space:
    # fatal error: libtool: can't vm_allocate() buffer for output file:
    # /opt/local/var/macports/build/_opt_PPCSnowLeopardPorts_devel_nodejs14/nodejs14/work/node-v14.21.3/out/Release/libv8_initializers.a
    # of size 1513436264 ((os/kern) no space available)
    depends_lib-append  port:brotli \
                        port:c-ares
    configure.args-append \
                        --enable-lto=False \
                        --enable-static=False \
                        --shared-brotli \
                        --shared-cares \
                        --v8-enable-object-print=False \
                        --v8-lite-mode \
                        --v8-non-optimized-debug=True \
                        --without-dtrace
}

universal_variant       no

# "V8 doesn't like cache."
configure.ccache        no

test.run                yes
test.cmd                ${build.cmd} -j${build.jobs}

switch $build_arch {
    i386 {
        configure.args-append   --dest-cpu=ia32
    }
    x86_64 {
        configure.args-append   --dest-cpu=x64
    }
    arm64 {
        configure.args-append   --dest-cpu=arm64
    }
    ppc {
        configure.args-append   --dest-cpu=ppc
    }
    ppc64 {
        configure.args-append   --dest-cpu=ppc64
    }
}

compiler.blacklist-append {clang < 900}

build.args-append   CC=${configure.cc} \
                    CXX=${configure.cxx} \
                    CXX.host=${configure.cxx} \
                    CPP=${configure.cpp} \
                    CFLAGS="${configure.cflags}" \
                    CXXFLAGS="${configure.cxxflags}" \
                    LDFLAGS="${configure.ldflags} [legacysupport::get_library_link_flags]" \
                    PYTHON=${configure.python} \
                    V=1

destroot {
    set bindir ${destroot}${prefix}/bin
    set libdir ${destroot}${prefix}/lib
    set libndir ${libdir}/node
    set libddir ${libdir}/dtrace
    set incdir ${destroot}${prefix}/include/node
    set docdir ${destroot}${prefix}/share/doc/${name}

    xinstall -d ${bindir}
    xinstall -d ${libdir}
    xinstall -d ${libndir}
    xinstall -d ${libddir}
    xinstall -d ${incdir}
    xinstall -d ${docdir}

    # install binaries
    xinstall -m 755 -W ${worksrcpath} \
        out/Release/node \
        ${bindir}

    # install headers
    xinstall -m 644 {*}[glob ${worksrcpath}/src/*.h]                ${incdir}
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/v8/include/*.h]    ${incdir}
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/uv/include/*.h]    ${incdir}
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/cares/include/*.h] ${incdir}

    # install dtrace script
    xinstall -m 644 -W ${worksrcpath} \
        src/node.d \
        ${libddir}

    # install manpage
    xinstall -m 644 -W ${worksrcpath} \
        doc/node.1 \
        ${destroot}${prefix}/share/man/man1

    # install docs
    xinstall -m 644 -W ${worksrcpath} \
        AUTHORS \
        CHANGELOG.md \
        CONTRIBUTING.md \
        GOVERNANCE.md \
        LICENSE \
        README.md \
        SECURITY.md \
        ${docdir}
}

livecheck.url       ${homepage}dist/
livecheck.type      regex
livecheck.regex     {v(14\.\d+\.\d+)}
