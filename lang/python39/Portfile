# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem 1.0
PortGroup select 1.0

name                python39

# Remember to keep py39-tkinter and py39-gdbmʼs versions synced with this
version             3.9.13

set branch          [join [lrange [split ${version} .] 0 1] .]
categories          lang
license             PSF
platforms           darwin
maintainers         {jmr @jmroot}

description         An interpreted, object-oriented programming language
long_description    Python is an interpreted, interactive, object-oriented \
                    programming language.

homepage            https://www.python.org/
master_sites        ${homepage}ftp/python/${version}/

distname            Python-${version}
use_xz              yes
checksums           md5 5e2411217b0060828d5f923eb422a3b8 \
                    rmd160 2dc56b2166e5ae1eb19506590fcd599285eb9065 \
                    sha256 125b0c598f1e15d2aa65406e83f792df7d171cdf38c16803b149994316a3080f

patchfiles          patch-setup.py.diff \
                    patch-Lib-cgi.py.diff \
                    patch-configure.diff \
                    patch-Lib-ctypes-macholib-dyld.py.diff \
                    patch-libedit.diff \
                    patch-configure-xcode4bug.diff \
                    sysconfig.py.diff

if {${os.platform} eq "darwin" && ${os.major} < 10} {
    # work around no copyfile and/or pthread_threadid_np on older systems
    patchfiles-append  patch-no-copyfile-on-Tiger.diff \
                       patch-threadid-older-systems.diff
}

if {${os.platform} eq "darwin" && ${os.major} == 10 && ${build_arch} eq "ppc"} {
    # this is needed for a PPC build on SL
    patchfiles-append  patch-threadid-older-systems.diff
}

depends_build       port:pkgconfig
depends_lib         port:bzip2 \
                    port:expat \
                    port:gettext-runtime \
                    port:libedit \
                    port:libffi \
                    port:ncurses \
                    path:lib/libssl.dylib:openssl \
                    port:sqlite3 \
                    port:xz \
                    port:zlib
depends_run         port:python_select \
                    port:python3_select

# blacklist llvm-gcc-4.2 compiler known to produce bad code
compiler.blacklist-append *llvm-gcc-4.2

# ensurepip arg may be removed later, now conflicts with pip and setuptools packages
configure.args      --enable-framework=${frameworks_dir} \
                    --enable-ipv6 \
                    --enable-loadable-sqlite-extensions \
                    --with-computed-gotos \
                    --with-ensurepip=no \
                    --with-system-expat \
                    --with-system-ffi

configure.ccache    no
# pkg-config removes -I flags for paths in CPATH, which confuses python.
configure.env       PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1 \
                    SETUPTOOLS_USE_DISTUTILS=stdlib
build.env           SETUPTOOLS_USE_DISTUTILS=stdlib
destroot.env        SETUPTOOLS_USE_DISTUTILS=stdlib

post-patch {
    reinplace "s|@@PREFIX@@|${prefix}|g" \
      ${worksrcpath}/Lib/cgi.py \
      ${worksrcpath}/Lib/ctypes/macholib/dyld.py

    reinplace "s|/setup.py|/setup.py --no-user-cfg|" ${worksrcpath}/Makefile.pre.in

    # replace /Applications with ${applications_dir}
    reinplace "s|@@APPLICATIONS_DIR@@|${applications_dir}|" \
      ${worksrcpath}/configure
}

build.target        all

test.run            yes
test.target         test

destroot.target     frameworkinstall maninstall

set pythonNoDot python[string map {. {}} $branch]
select.entries      [list python python-$pythonNoDot $pythonNoDot] \
                    [list python3 python3-$pythonNoDot $pythonNoDot]

platform darwin {
    set framewpath  ${frameworks_dir}/Python.framework
    set framewdir   ${framewpath}/Versions/${branch}
    set confdir     config-${branch}-darwin

	if {![variant_isset universal]} {
		post-configure {
			# poll() misbehaves on 10.8 and older
			# See https://trac.macports.org/ticket/18376
			if {${os.major} <= 12} {
				set oldmtime [file mtime ${worksrcpath}/pyconfig.h]
				system -W ${worksrcpath} "ed - pyconfig.h < ${filespath}/pyconfig.ed"
				file mtime ${worksrcpath}/pyconfig.h $oldmtime
			}
		}
	
		post-build {
			set buildlibdir [lindex [glob -directory ${worksrcpath}/build lib.*-*-*-${branch}] 0]
			# preserve mtime of sysconfig data file to avoid rebuilding things after changing it
			set oldmtime [file mtime ${buildlibdir}/_sysconfigdata__darwin_darwin.py]
	
			# Without this, LINKFORSHARED is set to
			# ... $(PYTHONFRAMEWORKDIR)/Versions/$(VERSION)/$(PYTHONFRAMEWORK)
			# (this becomes Python.framework/Versions/3.9/Python) which doesnʼt
			# work for dependents that incorrectly use this variable to find out
			# how to link against python (see ticket #15099); instead we mirror
			# the behavior of `python-config --ldflags` here.
			set lfs_pattern {^([[:space:]]*'LINKFORSHARED':).*}
			set lfs_replacement "\\1 '-L${framewdir}/lib/python${branch}/${confdir} -lpython${branch} -ldl -framework CoreFoundation',"
			reinplace -E s|${lfs_pattern}|${lfs_replacement}| \
				${buildlibdir}/_sysconfigdata__darwin_darwin.py
	
			# remove -arch flags from the config
			reinplace -E {s|-arch [a-z0-9_]+||g} \
				${buildlibdir}/_sysconfigdata__darwin_darwin.py
	
			# also remove gettext overlinking
			reinplace "s|-lintl||" \
				${buildlibdir}/_sysconfigdata__darwin_darwin.py
	
			file mtime ${buildlibdir}/_sysconfigdata__darwin_darwin.py $oldmtime
		}

	} else {
        # universal variant
		post-configure {
			if {${os.major} <= 12} {
				set oldmtime [file mtime ${worksrcpath}-${build_arch}/pyconfig.h]
				system -W ${worksrcpath}-${build_arch} "ed - pyconfig.h < ${filespath}/pyconfig.ed"
				file mtime ${worksrcpath}-${build_arch}/pyconfig.h $oldmtime
			}
		}
	
		post-build {
			set buildlibdir [lindex [glob -directory ${worksrcpath}-${build_arch}/build lib.*-*-*-${branch}] 0]
			set oldmtime [file mtime ${buildlibdir}/_sysconfigdata__darwin_darwin.py]
	
			set lfs_pattern {^([[:space:]]*'LINKFORSHARED':).*}
			set lfs_replacement "\\1 '-L${framewdir}/lib/python${branch}/${confdir} -lpython${branch} -ldl -framework CoreFoundation',"
			reinplace -E s|${lfs_pattern}|${lfs_replacement}| \
				${buildlibdir}/_sysconfigdata__darwin_darwin.py
	
			reinplace -E {s|-arch [a-z0-9_]+||g} \
				${buildlibdir}/_sysconfigdata__darwin_darwin.py
	
			reinplace "s|-lintl||" \
				${buildlibdir}/_sysconfigdata__darwin_darwin.py
	
			file mtime ${buildlibdir}/_sysconfigdata__darwin_darwin.py $oldmtime
		}
	}
}

post-destroot {
    foreach unversioned {2to3 idle3 pydoc3 python3 python3-config} {
        delete ${destroot}${prefix}/bin/${unversioned}
    }
}

notes "
To make this the default Python or Python 3 (i.e., the version run by\
the 'python' or 'python3' commands), run one or both of:

    sudo port select --set python $pythonNoDot
    sudo port select --set python3 $pythonNoDot
"

variant optimizations description {enable expensive, stable optimizations (including PGO)} {
    configure.args-append   --enable-optimizations
}

variant lto description {enable Link-Time Optimization} {
    configure.args-append   --with-lto
    if {${os.major} < 11} {
    patchfiles-append       patch-enable_dynamic.diff
    # lto requires a newer compiler
    compiler.blacklist-append *clang* *gcc-4.0 *gcc-4.2
    }
}

platform darwin {
    # Build failures on 10.9 and older
    if {${os.major} > 11} {
        if {${os.major} > 14 || !($universal_possible && [variant_isset universal])} {
            default_variants +lto
        }
        if {${os.major} > 13} {
            default_variants-append +optimizations
        }
    }
}

variant dtrace description {enable DTrace support} {
    configure.args-append   --with-dtrace
}

livecheck.type      regex
livecheck.url       ${homepage}downloads/source/
livecheck.regex     Python (${branch}\[.0-9\]+) -
